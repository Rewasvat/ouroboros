//=========================// CLASS SPECIFICATION //

class_specifier:
    class_head '{' {
        d_scanner.ChangeScope(nullptr);
    } '}' {
        md::Ptr<md::Class> classObj = ($1).first;
        md::NestedNameSpecifier nestedName = ($1).second;
        $$ = parser::AddClassToScope(classObj, nestedName);
    }
|
    class_head '{' member_specification { d_scanner.ChangeScope(nullptr); } '}' {
        md::Ptr<md::Class> classObj = ($1).first;
        md::NestedNameSpecifier nestedName = ($1).second;
        for (auto it: *$3)
            if (!(it)(classObj))
                throw md::SemanticError("Error executing ScopeAction - member for class", __FILE__, __LINE__);
        $$ = parser::AddClassToScope(classObj, nestedName);
    }
;

class_head:
    class_key {
        $$ = parser::ClassHeadPair(md::Class::Create("", BaseSpecifierList()),
                                   md::NestedNameSpecifier(""));
        d_scanner.ChangeScope($$);
    }
|
    class_key base_clause { 
        $$ = parser::ClassHeadPair(md::Class::Create("", *$2),
                                   md::NestedNameSpecifier(""));
        d_scanner.ChangeScope($$);
    }
|
    class_key class_head_name {
        $$ = parser::ClassHeadPair(md::Class::Create(($2).name(), BaseSpecifierList()), $2);
        d_scanner.ChangeScope($$);
    }
|
    class_key class_head_name base_clause {
        $$ = parser::ClassHeadPair(md::Class::Create(($2).name(), *$3), $2);
        d_scanner.ChangeScope($$);
    }
;

class_head_name:
    class_name { $$ = md::NestedNameSpecifier($1); }
|
    nested_name_specifier class_name { $$ = $1;
        ($$).set_name($2);
    }
;

base_clause:
    ':' base_specifier_list { $$ = $2; }
;

base_specifier_list:
    base_specifier {
        $$ = std::shared_ptr<BaseSpecifierList>(new BaseSpecifierList);
        ($$)->push_back($1);
    }
|
    base_specifier_list ',' base_specifier {
        ($1)->push_back($3);
        $$ = $1;
    }
;

base_specifier:
    base_type_specifier { $$ = parser::BaseSpecifier($1); }
|
    VIRTUAL base_type_specifier { $$ = parser::BaseSpecifier($2, true); }
|
    access_specifier base_type_specifier { $$ = parser::BaseSpecifier($2, $1); }
|
    VIRTUAL access_specifier base_type_specifier { $$ = parser::BaseSpecifier($3, true, $2); }
|
    access_specifier VIRTUAL base_type_specifier { $$ = parser::BaseSpecifier($3, true, $1); }
;

base_type_specifier:
    class_name { $$ = md::NestedNameSpecifier($1); }
|
    SCOPE_OPERATOR class_name { $$ = md::NestedNameSpecifier($2, true); }
|
    nested_name_specifier class_name { $$ = $1;
        ($$).set_name($2);
    }
|
    SCOPE_OPERATOR nested_name_specifier class_name { $$ = $2;
        ($$).set_name($3);
        ($$).set_global(true);
    }
;

class_name:
    identifier { $$ = $1; }
;

class_key:
    CLASS | UNION | STRUCT
;
